buildscript {
    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath "io.github.kdabir.directree:directree:+"
        classpath "gradle.plugin.io.github.kdabir.gradlehelpers:gradle-helpers:0.0.3"
    }
}

plugins {
    id "com.kdabir.mksrc" version "1.0.0" apply false
}


apply plugin: 'idea'

import directree.DirTree

ext.groovyVersion = "2.5.3"


idea {
    module {
        excludeDirs += [file(".gradle"), file('gradle'), file(".idea")]
    }
}


subprojects {
    apply plugin: 'maven'
    apply plugin: 'groovy'
    apply plugin: 'idea'
    apply plugin: 'com.kdabir.mksrc'

    sourceCompatibility = 1.8 // java 8
    targetCompatibility = 1.8

    repositories {
        jcenter()
    }

    plugins.withType(GroovyPlugin) {

        dependencies {
            compile "org.codehaus.groovy:groovy-all:${groovyVersion}"

            testCompile 'org.spockframework:spock-core:1.0-groovy-2.4', { exclude module: 'groovy-all' }
        }

    }

    idea {
        module {
            excludeDirs += [file('build')]
            outputDir file('build/idea/main')
            testOutputDir file('build/idea/test')
        }
    }

    test {
        testLogging {
            events 'failed'
            exceptionFormat 'short'
        }
    }
}


class CreateModule extends DefaultTask {
    // Use --name filename.
    @org.gradle.api.tasks.options.Option(
        option = "name",
        description = "module name"
    )
    String moduleName

    CreateModule() {
        moduleName = 'newmodule' // default name
        description = 'generates a new module, overwrites if already exists'
    }

    @OutputDirectory
    File getOutputFile() {
        return new File(project.rootDir, moduleName)
    }

    @TaskAction
    void create() {
        def name = moduleName // so that its available inside following closure
        DirTree.create(outputFile.absolutePath) {
            file("${name}.gradle", "//.keep")
            file("README.md", "# ${name}")
        }
        this.project.file("settings.gradle").append("\ninclude ':${name}'")
    }
}

task createModule(type: CreateModule)

task build

// gradle createModule --name epoch
// gradle epoch:setupDirs
// find . -type d -empty -delete


allprojects {
    apply plugin: 'idea'
}
